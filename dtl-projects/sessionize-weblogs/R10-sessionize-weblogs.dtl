/DTL
    /TASKTYPE SORT
    /INFILE ${WORKSPACE_DIR}/sorted-logs-by-partition.dat STLF FIELDSEPARATOR X"09" LAYOUT sorted-logs-by-partition-layout
    /KEYS ip, utc-timestamp
    /DELIMITEDRECORDLAYOUT sorted-logs-by-partition-layout {
	partition-num EN,
	ip,
	utc-timestamp DATETIME (YEARMM0DD0 HH0MI0SE0),
	count EN,
	size EN,
	referrer-url,
	user-agent,
	country-name
    }
    /OUTFILE ${WORKSPACE_DIR}/sessionized-weblogs.dat STLF OVERWRITE
    /REFORMAT CREATE LAYOUT sessionized-weblogs-layout DELIMITED VALUES 
    sessionID,
    ip,
    utc-timestamp (YEARMM0DD0 HH0MI0SE0),
    size EN,
    referrer-url,
    user-agent,
    count EN,
    country-name
    /COLLATINGSEQUENCE DEFAULT ASCII
    /* Determine if there is a key break (new user == new IP) */ 
    /CONDITION isNewSession ip != GetUserDefinedValue("lastIP") or DateDiff(utc-timestamp, ToDate(GetUserDefinedValue("lastTimestamp"), "YEARMM0DD0 HH0MI0SE0"), "minute") > 10
    /VALUE getSessionID ToNumber(GetUserDefinedValue("sessionID"))
    /VALUE incrementSessionID ToNumber(SetUserDefinedValue("sessionID", getSessionID + 1))
    /VALUE sessionID EvaluateExpressionList(
    IfThenElse(isNewSession,incrementSessionID,0),
    setValues,
    getSessionID
    )
    /VALUE setValues EvaluateExpressionList(
    SetUserDefinedValue("lastIP", ip),
    SetUserDefinedValue("lastTimestamp", utc-timestamp)
    )
/END